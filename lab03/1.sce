
x0 = 55000;//численность первой армии
y0 = 55000;//численность второй армии
t0 = 0;//начальный момент времени
a = 0.3;//константа, характеризующая степень влияния различных факторов на потери
b = 0.69;//эффективность боевых действий армии у
c = 0.7;//эффективность боевых действий армии х
h = 0.29;//константа, характеризующая степень влияния различных факторов на потери
tmax = 1;//предельный момент времени
dt = 0.05;//шаг изменения времени
t = [t0:dt:tmax];
function p = P(t)//возможность подхода подкрепления к армии х
p = sin(t);
endfunction
function q = Q(t)//возможность подхода подкрепления к армии у
q = cos(t);
endfunction

function dy = syst(t, y)
dy(1) = - a*y(1) - b*y(2) + P(t);//изменение численности первой армии
dy(2) = - c*y(1) - h*y(2) + Q(t);//изменение численности второй армии
endfunction
v0 = [x0;y0];//Вектор начальных условий

y = ode(v0,t0,t,syst);

scf(0);
plot2d(t,y(1,:),style=2);//График изменения численности армии х (синий)
xtitle('Модель боевых действий № 1','Шаг','Численность армии');
plot2d(t,y(2,:), style = 5);//График изменения численности армии у (красный)
xgrid();
